plugins {
    id "net.neoforged.moddev.legacyforge"
    id "maven-publish"
    id 'com.palantir.git-version' version '3.1.0'
}

version = gitVersion()
group = mod_group
archivesBaseName = mod_id

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://maven.latvian.dev/releases'
        content {
            includeGroup 'dev.latvian.mods'
            includeGroup 'dev.latvian.apps'
        }
    }
    maven {
        url 'https://maven.architectury.dev/'
        content { includeGroup 'dev.architectury' }
    }
    maven {
        url 'https://api.modrinth.com/maven'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.1.0'
    // KubeJS - soft dependency for scripting integration
    compileOnly 'dev.latvian.mods:kubejs-forge:2001.6.5-build.16'
    modImplementation 'maven.modrinth:minecraft-sqlite-jdbc:3.51.1.0+2025-12-01'
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

    runs {
        configureEach {
            gameDirectory = project.file('run')
        }
        client {
            client()
        }
        server {
            server()
        }
    }
}

processResources {
    var expansions = [
        "mod_id": mod_id,
        "mod_name": mod_name,
        "mod_version": version,
        "minecraft_version_range": minecraft_version_range,
        "forge_version_range": forge_version_range
    ]

    inputs.properties(expansions)

    filesMatching("META-INF/mods.toml") {
        expand expansions
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
